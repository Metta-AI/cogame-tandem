## Deterministic replay state shared by the native host and the WASM viewer:
## `initReplayRuntime` / `advanceReplayFrame` / `buildReplayViewerPacket`.
## Kept from ctf, byte-identical apart from the imports and the chrome payload.

import
  std/json,
  bitworld/spriteprotocol,
  broadcast, global, replays, sim

type
  InitializedReplay* = object
    config*: GameConfig
    sim*: SimServer
    player*: ReplayPlayer
    tracker*: BroadcastTracker

proc initReplayRuntime*(
  data: ReplayData,
  mismatchQuit: bool,
  gameEventLoggingEnabled = true
): InitializedReplay =
  ## Constructs and starts replay playback from the RECORDED game config.
  result.config = defaultGameConfig()
  result.config.update(data.configJson)
  result.sim = initSimServer(result.config)
  # The viewer READS the recorded course rather than regenerating it, so a
  # future change to `generateCourse` can never desynchronise an old replay.
  # Generation is only the fallback for a header that carries none.
  result.sim.adoptCourse(recordedCourse(data.configJson))
  result.sim.gameEventLoggingEnabled = gameEventLoggingEnabled
  result.player = initReplayPlayer(data)
  result.player.mismatchQuit = mismatchQuit
  # The whole-match precompute walk starts here and advances a bounded slice
  # per presentation frame; only the short lobby walk to the first Playing
  # tick — the spectator start — is paid up front.
  result.player.initReplayScan(result.sim)
  while not result.sim.carrying() and result.sim.phase != GameOver and
      result.sim.tickCount < result.player.replayMaxTick() and
      result.player.hashIndex < result.player.data.hashes.len and
      not result.player.hashValidationFailed:
    result.player.stepReplay(result.sim)
  if result.player.startTick < 0 and result.sim.carrying():
    result.player.startTick = result.sim.gameStartTick
  result.player.seekReplay(result.sim, result.player.replayStartTick())
  result.player.playing = true
  result.tracker = initBroadcastTracker()

proc advanceReplayFrame*(
  replay: var ReplayPlayer,
  sim: var SimServer,
  tracker: var BroadcastTracker,
  seekTicks: openArray[int],
  commands: openArray[char]
): JsonNode =
  ## Applies viewer controls and advances one public presentation frame.
  var didSeek = false
  for seekTick in seekTicks:
    replay.applyReplaySeek(sim, seekTick)
    didSeek = true
  for command in commands:
    let tickBeforeCommand = sim.tickCount
    replay.applyReplayCommand(sim, command)
    if sim.tickCount != tickBeforeCommand:
      didSeek = true
  if didSeek:
    tracker.resync(sim)
    replay.cancelEndHold()

  let events = newJArray()
  let
    simPtr = sim.addr
    trackerPtr = tracker.addr
  replay.advanceReplayPlayback(
    sim,
    proc () = simPtr[].stepEvents(trackerPtr[], events),
    proc () = trackerPtr[].resync(simPtr[])
  )
  result = events

proc buildReplayViewerPacket*(
  sim: var SimServer,
  replay: ReplayPlayer,
  state: GlobalViewerState,
  nextState: var GlobalViewerState,
  events: JsonNode
): seq[uint8] =
  ## The shared replay board plus the chrome packet for one viewer.
  result = sim.buildSpriteProtocolUpdates(
    state,
    nextState,
    sim.tickCount,
    replay.playing,
    replay.replaySpeed(),
    replay.replayMaxTick(),
    replay.looping,
    true,
    replay.hashMismatchTick
  )
  # The lead chrome (goal series, beat markers, lull spans) waits for the
  # background precompute walk: it ships ONCE per viewer, so sending before the
  # walk finishes would freeze a half-scanned timeline into the HUD.
  let sendLead = not state.momentumSent and replay.scanComplete
  result.addSprite(
    BroadcastChromeSpriteId,
    1,
    1,
    [0'u8, 0, 0, 0],
    sim.buildStateJson(
      events,
      replay.playing,
      replay.replayDisplaySpeed(),
      replay.replayMaxTick(),
      replay.looping,
      true,
      replay.hashMismatchTick,
      nextState.selectedJoinOrder,
      if sendLead: replay.leadSeries else: @[],
      replay.replayStartTick(),
      replay.endHoldSecondsLeft(),
      false,
      replay.skipLulls,
      replay.skipLulls and replay.playing and
        replay.isLullTick(sim.tickCount),
      if sendLead: replay.lullSpans else: @[],
      if sendLead: replay.beatEvents else: nil
    )
  )
  if sendLead:
    nextState.momentumSent = true
